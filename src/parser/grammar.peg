start := start=line

line := '[\s]*' label=label? '[\s]*' currentLine=directive wso comment=comment? nend nextLine=line |
        '[\s]*' label=label? '[\s]*' currentLine=instruction wso comment=comment? nend nextLine=line |
        '[\s]*' label=label? '[\s]*' currentLine=commentLine nend nextLine=line |
        '[\s]*' $

//directive := directive='.arm' | directive='.text' | directive='.data' | directive='.align' | directive='.global' ws '_start'
directive := directive=ascii
ascii := '.ascii' ws '"' data='[ -!#-~]*' '"'
label := label='[_A-Za-z0-9]+' ':'
instruction := instruction=art | instruction=log | instruction=copyJump | instruction=loadStore | instruction=softwareInterrupt
commentLine := commentLine=comment
emptyLine := emptyLine=wso

comment := comment='//[ \t\S]*' | comment='\/\*[ \t\S]*\*\/'

//------------------------------------------------------------------------------------------------

art := inst=artInst cond=condition ws operands=artOp | inst='mul' cond=condition ws operands=artMulOp | inst='mla' cond=condition ws operands=artMlaOp

artInst := 'add' | 'adc' | 'sub' | 'sbc' | 'rsb' | 'rsc'

artOp := artOp3 | artOp2
artOp2 := op1=regOp wso ',' wso op2=op
artOp3 := op1=regOp wso ',' wso op2=regOp wso ',' wso op3=op

artMulOp := op1=regOp wso ',' wso op2=regOp wso ',' wso op3=regOp
artMlaOp := op1=regOp wso ',' wso op2=regOp wso ',' wso op3=regOp wso ',' wso op4=regOp

//------------------------------------------------------------------------------------------------

log := inst=logInst cond=condition ws operands=logOp | inst=logCmpInst cond=condition ws operands=logOp2

logInst := 'and' | 'orr' | 'eor' | 'bic'
logCmpInst := 'cmp' | 'cmn' | 'tst' | 'teq'

logOp := logOp3 | logOp2
logOp2 := op1=regOp wso ',' wso op2=op
logOp3 := op1=regOp wso ',' wso op2=regOp wso ',' wso op3=op

//------------------------------------------------------------------------------------------------

copyJump := inst=copyInst cond=condition ws operands=copyOp | inst=jumpInst1 cond=condition ws operands=jumpOp | inst=jumpInst2 cond=condition ws operands=jumpOp

copyInst := 'mov' | 'mvn'
jumpInst1 := 'b'
jumpInst2 := 'bl'

copyOp := op1=regOp wso ',' wso op2=op
jumpOp := op1=branchOp

//------------------------------------------------------------------------------------------------

loadStore := inst=loadStoreInst cond=condition ws operands=loadStoreOp | inst=loadStoreInst cond=condition ws operands=loadImmediateOp | inst=loadStoreInst cond=condition ws operands=loadImmediateBranchOp

loadStoreInst := 'ldr' | 'str'

loadStoreOp := op1=regOp wso ',' wso op2=addressingMode
loadImmediateOp := op1=regOp wso ',' wso op2=immOp
loadImmediateBranchOp := op1=regOp wso ',' wso '=' op2=branchOp

//------------------------------------------------------------------------------------------------

softwareInterrupt := inst='swi' cond=condition ws operands='#0'

//------------------------------------------------------------------------------------------------

op := shiftOp=shiftOp | regImmOp=regImmOp
regImmOp := regOp=regOp | immOp=immOp
shiftOp := opToShift=regImmOp wso ',' wso shiftType=shiftType wso opShift=regImmOp
addressingMode := '\[' wso reg=regOp wso '\]' offset=offset? | '\[' wso reg=regOp offset=offset? wso '\]' increment='!'?

offset := wso ',' wso sign=sign offset=op

regOp := regOp='r[0-9]+' | 'pc' | 'lr'

immOp := immType=immType sign=sign base=base number='[0-9a-f]+'
immType := '#' | '='
base := '0x' | '0b' | '0o' | ''
sign := '-' | '\+' | ''

branchOp := '[_A-Za-z0-9]+'

shiftType := 'lsl' | 'asl' | 'lsr' | 'asr' | 'ror' | 'rrx'

condition := condType=conditionType updateStatusReg='s'?
conditionType := 'eq' | 'ne' | 'hs' | 'cs' | 'lo' | 'cc' | 'mi' | 'pl' | 'vs' | 'vc' | 'hi' | 'ls' | 'ge' | 'lt' | 'gt' | 'le' | 'al' | 'nv' | ''

//------------------------------------------------------------------------------------------------

ws := '[ \t]+'
wso := '[ \t]*'
nend := '\n' | $